{ stdenv, fetchFromGitHub }:

stdenv.mkDerivation rec {
  name = "google-fonts-${version}";
  version = "2019-02-21";

  src = fetchFromGitHub {
    owner = "google";
    repo = "fonts";
    rev = "ab442e488a2962f4e520a7a384ba1926d3db1994";
    sha256 = "0m68ihffd4w8gmpl1xv4qi7ph1sq8sszw15wf8mkdxcp2g9yh89b";
  };

  outputHashAlgo = "sha256";
  outputHashMode = "recursive";
  outputHash = "088yb05qfdwhwl2vijwlqc10spd23pp0k6f8z4imjqm4djlh48r3";

  phases = [ "unpackPhase" "patchPhase" "installPhase" ];

  patchPhase = ''
    # These directories need to be removed because they contain
    # older or duplicate versions of fonts also present in other
    # directories. This causes non-determinism in the install since
    # the installation order of font files with the same name is not
    # fixed.
    rm -rv ofl/alefhebrew \
      ofl/misssaintdelafield \
      ofl/mrbedford \
      ofl/siamreap \
      ofl/terminaldosislight

    # Remove 'static' versions of variable fonts, avoid conflict
    rm ofl/*/static -rf

    if find . -name "*.ttf" | sed 's|.*/||' | sort | uniq -c | sort -n | grep -v '^.*1 '; then
      echo "error: duplicate font names"
      exit 1
    fi
  '';

  installPhase = ''
    dest=$out/share/fonts/truetype
    find . -name '*.ttf' -exec install -m 444 -Dt $dest '{}' +
  '';

  meta = with stdenv.lib; {
    homepage = https://fonts.google.com;
    description = "Font files available from Google Fonts";
    license = with licenses; [ asl20 ofl ufl ];
    platforms = platforms.all;
    hydraPlatforms = [];
    maintainers = with maintainers; [ manveru ];
  };
}
